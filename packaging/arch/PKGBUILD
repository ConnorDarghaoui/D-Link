# Maintainer: Lucas Boniche <noreply@localhost>
pkgname=d-link
pkgver=0.1.0
pkgrel=1
pkgdesc="TI-Nspire calculator manager via USB"
arch=('x86_64')
url="https://github.com/ConnorDarghaoui/D-Link"
license=('MIT')
depends=(
    'webkit2gtk-4.1'
    'libusb'
    'gtk3'
    'cairo'
    'gdk-pixbuf2'
    'glib2'
    'hicolor-icon-theme'
)
makedepends=(
    'rust'
    'cargo'
    'nodejs'
    'pnpm'
    'pkg-config'
)
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('SKIP')

prepare() {
    cd "D-Link-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
    cd "D-Link-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    pnpm install
    pnpm build
    cd src-tauri
    cargo build --release --locked
}

package() {
    cd "D-Link-$pkgver"
    
    # Binario
    install -Dm755 "src-tauri/target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
    
    # Iconos
    install -Dm644 "src-tauri/icons/icon.png" "$pkgdir/usr/share/icons/hicolor/256x256/apps/$pkgname.png"
    install -Dm644 "src-tauri/icons/32x32.png" "$pkgdir/usr/share/icons/hicolor/32x32/apps/$pkgname.png"
    install -Dm644 "src-tauri/icons/128x128.png" "$pkgdir/usr/share/icons/hicolor/128x128/apps/$pkgname.png"
    
    # Desktop entry
    install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/$pkgname.desktop" <<EOF
[Desktop Entry]
Type=Application
Name=D-Link
GenericName=TI-Nspire Manager
Comment=Manage TI-Nspire calculators via USB
Exec=$pkgname
Icon=$pkgname
Terminal=false
Categories=Utility;Development;
StartupWMClass=$pkgname
Keywords=TI;Nspire;Calculator;USB;
EOF

    # Reglas udev para acceso USB sin root
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/udev/rules.d/69-$pkgname.rules" <<EOF
# TI-Nspire calculators - permite acceso sin root
# TI-Nspire CX / CX CAS
SUBSYSTEM=="usb", ATTR{idVendor}=="0451", ATTR{idProduct}=="e012", MODE="0666", TAG+="uaccess"
# TI-Nspire CX II / CX II CAS
SUBSYSTEM=="usb", ATTR{idVendor}=="0451", ATTR{idProduct}=="e022", MODE="0666", TAG+="uaccess"
EOF
}

